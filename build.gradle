plugins {
    id 'org.jetbrains.intellij' version '1.6.0'
    id 'io.freefair.lombok' version '6.5.0.2'
    id 'java'
}

group 'com.checkmarx'
version System.getenv('RELEASE_VERSION') ?: "dev"

def remoteRobotVersion = '0.11.6'
def jUnitVersion = '5.7.2'

repositories {
    mavenCentral()

    maven {
        url = 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
}

dependencies {
    testImplementation 'com.intellij.remoterobot:remote-robot:' + remoteRobotVersion
    testImplementation 'com.intellij.remoterobot:remote-fixtures:' + remoteRobotVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:' + jUnitVersion
    // Video Recording
    testImplementation 'com.automation-remarks:video-recorder-junit5:2.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + jUnitVersion

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    implementation 'com.miglayout:miglayout-swing:11.0'

    implementation 'com.checkmarx.ast:ast-cli-java-wrapper:1.0.34'
    implementation(platform("com.fasterxml.jackson:jackson-bom:2.13.3")) // Temporary workaround https://github.com/FasterXML/jackson-databind/issues/3428

}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.1.1'
    updateSinceUntilBuild = false
}

runPluginVerifier {
    ideVersions = ['IC-2021.1.1', 'IC-2021.2', 'IC-2021.3']
}

downloadRobotServerPlugin {
    version = remoteRobotVersion
}

test {
    useJUnitPlatform()
    systemProperty 'uiWaitDuration', project.findProperty('uiWaitDuration') ?: 300
    testLogging {
        showStandardStreams = true
    }
}

runIdeForUiTests {
    systemProperty 'jb.privacy.policy.text', '<!--999.999-->'
    systemProperty 'jb.consents.confirmation.enabled', 'false'
}

publishPlugin {
    token.set System.getenv("PUBLISH_TOKEN")
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

patchPluginXml {
    sinceBuild = '211.6693.111'
}
