plugins {
    id 'io.freefair.lombok' version '8.4'
    id 'org.jetbrains.intellij' version '1.16.0'
    id 'java'
}

group 'com.checkmarx'
version System.getenv('RELEASE_VERSION') ?: "dev"

def javaWrapperVersion = System.getenv('JAVA_WRAPPER_VERSION')
def remoteRobotVersion = '0.11.16'

repositories {
    mavenCentral()
    maven {
        url = 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    testImplementation('com.intellij.remoterobot:remote-robot:' + remoteRobotVersion) {
        exclude group: 'com.squareup.okio', module: 'okio'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    testImplementation 'com.squareup.okio:okio:3.4.0'
    testImplementation 'com.google.guava:guava:32.0.0-android'
    testImplementation 'com.squareup.okhttp3:okhttp:4.9.2'

    testImplementation 'com.intellij.remoterobot:remote-fixtures:' + remoteRobotVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'junit:junit:4.13.1'
    // Video Recording
    testImplementation('com.automation-remarks:video-recorder-junit5:1.8') {
        exclude group: 'log4j', module: 'log4j'
    }
    testImplementation 'org.apache.logging.log4j:log4j-core:2.23.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    implementation 'com.miglayout:miglayout-swing:11.3'

    if (javaWrapperVersion == "" || javaWrapperVersion == null) {
        implementation ('com.checkmarx.ast:ast-cli-java-wrapper:1.0.64'){
            exclude group: 'junit', module: 'junit'
        }
    } else {
        implementation ('com.checkmarx.ast:ast-cli-java-wrapper:' + javaWrapperVersion){
            exclude group: 'junit', module: 'junit'
        }
    }
    testImplementation 'junit:junit:4.11-redhat-1'
    implementation(platform("com.fasterxml.jackson:jackson-bom:2.17.0"))
    // Temporary workaround https://github.com/FasterXML/jackson-databind/issues/3428
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2022.2.1'
    updateSinceUntilBuild = false
}

runPluginVerifier {
    ideVersions = ['IC-2021.1.1', 'IC-2021.2', 'IC-2021.3', 'IC-2022.1', 'IC-2022.2']
}

downloadRobotServerPlugin {
    version = remoteRobotVersion
}

test {
    useJUnitPlatform()
    systemProperty 'uiWaitDuration', project.findProperty('uiWaitDuration') ?: 300
    testLogging {
        showStandardStreams = true
    }
}

runIdeForUiTests {
    systemProperty 'jb.privacy.policy.text', '<!--999.999-->'
    systemProperty 'jb.consents.confirmation.enabled', 'false'
}

publishPlugin {
    token.set System.getenv("PUBLISH_TOKEN")
    if (project.hasProperty("rchannels")) {
        channels = [rchannels.toString()]
    }
}


java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

patchPluginXml {
    sinceBuild = '211.6693.111'
}
