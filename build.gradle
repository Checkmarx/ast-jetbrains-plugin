plugins {
    id 'io.freefair.lombok' version "${lombokPluginVersion}"
    id 'org.jetbrains.intellij' version "${intellijPluginVersion}"
    id 'java'
    id 'jacoco'
}

def javaWrapperVersion = System.getenv('JAVA_WRAPPER_VERSION') ?: defaultJavaWrapperVersion

// Common configuration for all projects
allprojects {
    group "${projectGroup}"
    version System.getenv('RELEASE_VERSION') ?: "dev"

    repositories {
        mavenCentral()
        maven {
            url = 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
        }
        maven {
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
}

// Common configuration for all submodules
subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(Integer.parseInt(javaVersion)))
        }
    }

    dependencies {
        testImplementation "com.intellij.remoterobot:remote-robot:${remoteRobotVersion}"
        testImplementation("com.intellij.remoterobot:remote-fixtures:${remoteRobotVersion}") {
            exclude group: 'com.square.okio', module: 'okio'
        }
        testImplementation "com.squareup.okio:okio:${okioVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitBomVersion}"
        testImplementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
        testImplementation 'junit:junit:4.11-redhat-1'
        testImplementation 'junit:junit:4.13.1'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.15.2'

        // Video Recording
        testImplementation('com.automation-remarks:video-recorder-junit5:2.0') {
            exclude group: 'log4j', module: 'log4j'
        }
        testImplementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
        testImplementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitEngineVersion}"

        // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
        implementation "com.miglayout:miglayout-swing:${miglayoutVersion}"

        implementation("com.checkmarx.ast:ast-cli-java-wrapper:${javaWrapperVersion}"){
            exclude group: 'junit', module: 'junit'
        }
        implementation(platform("com.fasterxml.jackson:jackson-bom:${jacksonBomVersion}"))
        // Temporary workaround https://github.com/FasterXML/jackson-databind/issues/3428

        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
        testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    }

    jacoco {
        toolVersion = "${jacocoVersion}"
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = "${intellijVersion}"
    updateSinceUntilBuild = false
}

runPluginVerifier {
    ideVersions = verifierIdeVersions.split(',').toList()
}

// Aggregated JaCoCo report for all modules
tasks.register('jacocoAggregatedReport', JacocoReport) {
    description = 'Generates an aggregated JaCoCo coverage report from all subprojects'
    group = 'verification'

    dependsOn subprojects*.test

    // Collect execution data from all subprojects
    executionData.from = files(subprojects.collect { project ->
        project.fileTree(dir: "${project.buildDir}/jacoco", include: '**/*.exec')
    })

    // Collect source and class files from all subprojects
    subprojects.each { project ->
        project.plugins.withType(JavaPlugin).whenPluginAdded {
            sourceDirectories.from project.files(project.sourceSets.main.allSource.srcDirs)
            classDirectories.from project.files(project.sourceSets.main.output)
        }
    }

    // Filter out execution data that doesn't exist
    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }

    reports {
        xml.required = true
        html.required = true
        csv.required = true
        xml.outputLocation = file("${buildDir}/reports/jacoco/aggregate/jacocoAggregatedReport.xml")
        html.outputLocation = file("${buildDir}/reports/jacoco/aggregate/html")
        csv.outputLocation = file("${buildDir}/reports/jacoco/aggregate/jacocoAggregatedReport.csv")
    }
}
