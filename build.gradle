plugins {
    id 'org.jetbrains.intellij' version '1.1.4'
    id 'io.freefair.lombok' version '6.1.0'
    id 'java'
}

group 'com.checkmarx'
version System.getenv('RELEASE_VERSION') ?: "dev"

repositories {
    mavenCentral()

    maven {
        name 'github'
        url 'https://maven.pkg.github.com/CheckmarxDev/ast-cli-java-wrapper'
        credentials {
            username System.getenv('GITHUB_ACTOR')
            password System.getenv('GITHUB_TOKEN')
        }
    }

    maven {
        url = 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
}

def remoteRobotVersion = '0.11.6'
def jUnitVersion = '5.7.2'
dependencies {
    testImplementation 'com.intellij.remoterobot:remote-robot:' + remoteRobotVersion
    testImplementation 'com.intellij.remoterobot:remote-fixtures:' + remoteRobotVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:' + jUnitVersion
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + jUnitVersion

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    implementation 'com.miglayout:miglayout-swing:11.0'

    implementation 'com.checkmarx.ast:ast-cli-java-wrapper:1.0.13'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.2'
}

runPluginVerifier {
    ideVersions = ['IC-2021.2']
}

downloadRobotServerPlugin {
    version = remoteRobotVersion
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

runIdeForUiTests {
    systemProperty('jb.privacy.policy.text', '<!--999.999-->')
    systemProperty('jb.consents.confirmation.enabled', 'false')
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
